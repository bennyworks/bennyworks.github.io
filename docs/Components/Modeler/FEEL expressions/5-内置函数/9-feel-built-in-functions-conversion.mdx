---
id: feel-built-in-functions-conversion
title: Conversion 函数
---

将值转换为不同的类型。

### string(from)

将给定的值作为字符串返回。

**函数声明**

```java
string(from: Any): string
```

**示例**

```java
string(1.1)
// "1.1"

string(date("2012-12-25"))
// "2012-12-25"
```

### number(from)

将给定的字符串解析为数字。

如果字符串不是数字，则返回`null`。

**函数声明**

```java
number(from: string): number
```

**示例**

```java
number("1500.5")
// 1500.5
```

### context(entries)

使用给定键值对的列表构造context。它是[`get entries()`](./6-feel-built-in-functions-context.mdx#get-entriescontext)的逆函数。

每个键值对必须是一个包含两个条目的context：`key`和`value`。名为`key`的条目必须具有`string`类型的值。

如果键相等，则可能会覆盖context条目。条目按照给定列表中的context顺序覆盖。

如果一个条目不是context，或者一个context不包含所需的条目，则返回`null`。

**函数声明**

```java
context(entries: list<context>): context
```

**示例**

```java
context([{"key":"a", "value":1}, {"key":"b", "value":2}])
// {a:1, b:2}
```

### date(from)

根据给定的值返回日期。

如果字符串不是有效的日历日期，则返回`null`。例如，`"2024-06-31"`是无效的，因为6月只有30天。

**函数声明**

```java
date(from: string): date
```

将给定的字符串解析为日期。

```java
date(from: date and time): date
```

从给定的日期和时间中提取日期组件。

**示例**

```java
date("2018-04-29")
// date("2018-04-29")

date(date and time("2012-12-25T11:00:00"))
// date("2012-12-25")
```

### date(year, month, day)

根据给定的数值返回一个日期。

如果数值不代表有效的日历日期，则返回`null`。例如，`2024,6,31`是无效的，因为6月只有30天。

**函数声明**

```java
date(year: number, month: number, day: number): date
```

**示例**

```java
date(2012, 12, 25)
// date("2012-12-25")
```

### time(from)

从给定的值返回时间。

**函数声明**

```java
time(from: string): time
```

将给定的字符串解析为时间。

```java
time(from: date and time): time
```

从给定的日期和时间中提取时间组件。

**示例**

```java
time("12:00:00")
// time("12:00:00")

time(date and time("2012-12-25T11:00:00"))
// time("11:00:00")
```

### time(hour, minute, second)

从给定的数值返回时间。

**函数声明**

```java
time(hour: number, minute: number, second: number): time
```

**示例**

```java
time(23, 59, 0)
// time("23:59:00")
```

### time(hour, minute, second, offset)

从给定的数值返回时间，包括时区偏移量。

**函数声明**

```java
time(hour: number, minute: number, second: number, offset: days and time duration): time
```

**示例**

```java
time(14, 30, 0, duration("PT1H"))
// time("14:30:00+01:00")
```

### date and time(from)

将给定的字符串解析为日期和时间。

如果字符串不是有效的日历日期，则返回 `null`。例如，`"2024-06-31T10:00:00"` 是无效的，因为6月只有30天。

**函数声明**

```java
date and time(from: string): date and time
```

**示例**

```java
date and time("2018-04-29T09:30:00")
// date and time("2018-04-29T09:30:00")
```

### date and time(date, time)

从给定的组件返回日期和时间。

**函数声明**

```java
date and time(date: date, time: time): date and time
```

```java
date and time(date: date and time, time: time): date and time
```

返回一个日期和时间值，该值由 `date` 的日期组件与 `time` 组合而成。

**示例**

```java
date and time(date("2012-12-24"),time("T23:59:00"))
// date and time("2012-12-24T23:59:00")

date and time(date and time("2012-12-25T11:00:00"),time("T23:59:00"))
// date and time("2012-12-25T23:59:00")
```

### date and time(date, timezone)

<Highlight color="#c54302">Camunda Extension</Highlight>

根据给定的时区返回给定日期和时间值。

如果`date` 的时区与 `timezone` 不同，则调整时间以匹配 `timezone` 的本地时间。

**函数声明**

```java
date and time(date: date and time, timezone: string): date and time
```

**示例**

```java
date and time(@"2020-07-31T14:27:30@Europe/Berlin", "America/Los_Angeles")
// date and time("2020-07-31T05:27:30@America/Los_Angeles")

date and time(@"2020-07-31T14:27:30", "Z")
// date and time("2020-07-31T12:27:30Z")
```

### duration(from)

将给定的字符串解析为持续时间。持续时间是天数和时间的持续时间，或者是年和月的持续时间。

**函数声明**

```java
duration(from: string): days and time duration
```

```java
duration(from: string): years and months duration
```

**示例**

```java
duration("P5D")
// duration("P5D")

duration("P32Y")
// duration("P32Y")
```

### years and months duration(from, to)

根据`from` 和 `to` 之间的年月返回年月持续时间。

**函数声明**

```java
years and months duration(from: date, to: date): years and months duration
```

**示例**

```java
years and months duration(date("2011-12-22"), date("2013-08-24"))
// duration("P1Y8M")
```
